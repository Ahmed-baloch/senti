//find which accounts failed to logon on computers where we identify a security detection
let detections = toscalar(SecurityDetection
    | summarize makeset(Computer));
SecurityEvent
| where Computer in (detections) and EventID == 4624
| summarize count() by Account

//Session Duration
SecurityEvent
| where EventID in (4624, 4634)
| project Computer, Account, TargetLogonId, TimeGenerated, EventID
| order by TimeGenerated asc, EventID asc
| summarize TimeList = makelist(TimeGenerated / 1s, 100000) by Computer, Account, TargetLogonId
| extend SessionDuration = series_fir(TimeList, dynamic([1, -1]), false, false)
| mvexpand SessionDuration limit 100000
| extend SessionDuration = todouble(SessionDuration)
| where SessionDuration != todouble(TimeList[0])
| project-away TimeList
| summarize count(), SessionDuration=avg(SessionDuration), dcount(TargetLogonId), dcount(Account) by Computer
| order by SessionDuration asc

