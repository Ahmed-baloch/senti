// Azure AD failed logon attempts 
SigninLogs
| where TimeGenerated >= ago(15d)
 | where ResultType in ('50126','50020') //Invalid username or password,The user doesn't exist in the tenant 
 | extend OS = DeviceDetail.operatingSystem, 
    Browser = DeviceDetail.browser,
    StatusCode = tostring(Status.errorCode), 
    StatusDetails = tostring(Status.additionalDetails), 
    State = tostring(LocationDetails.state), 
    City = tostring(LocationDetails.city)
 | summarize StartTime = min(TimeGenerated), 
    EndTime = max(TimeGenerated), 
    IPAddresses = makeset(IPAddress), 
    DistinctIPCount = dcount(IPAddress), 
    makeset(OS), makeset(Browser), makeset(City), 
    AttemptCount = count() by UserDisplayName, 
        UserPrincipalName, 
        AppDisplayName, 
        ResultType, 
        ResultDescription, 
        StatusCode, 
        StatusDetails, 
        Location, 
        State
| extend timestamp = StartTime, AccountCustomEntity = UserPrincipalName, IPADDR = tostring(IPAddresses[0])
| project timestamp,UserDisplayName, AppDisplayName, ResultType, IPADDR,AttemptCount,DistinctIPCount,ResultDescription
| sort by AttemptCount
